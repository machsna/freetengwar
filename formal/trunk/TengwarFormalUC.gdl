// This is free software: you can redistribute it and/or
// modify it under the terms of the GNU General Public License as
// published by the Free Software Foundation, either version 3 of
// the License, or (at your option) any later version.
//
// This program is distributed in the hope that it will be useful,
// but WITHOUT ANY WARRANTY; without even the implied warranty of
// MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
// GNU General Public License for more details.
// 
// You should have received a copy of the GNU General Public License
// along with this program.  If not, see <http://www.gnu.org/licenses/>.

#define ps postscript
#define LG_USENG 0x0409

environment { MUnits = 2048; }

#include "TengwarFormalUC.gdh"

table(feature)
/*
  silmepref {
    id=202;
    name.LG_USENG = string("Tehta on Silme");
    default = left;
    settings {
      left {
        value=0;
        name.LG_USENG = string("To the left");
      }
      right {
        value=3;
        name.LG_USENG = string("To the right");
      }
    }
  }
*/
  descwtengwa {
    id=204;
    name.LG_USENG = string("Descending W-tengwa");
    default = false;
  }
/*
  lowershook {
    id=205;
    name.LG_USENG = string("Prefer low S-hook");
    default = true;
  }
*/
  ossetail {
    id=207;
    name.LG_USENG = string("Marked Osse");
    default = none;
    settings {
      none {
        value=0;
        name.LG_USENG = string("Never");
      }
      final {
        value=1;
        name.LG_USENG = string("Word final");
      }
      all {
        value=2;
        name.LG_USENG = string("Always");
      }
    }
  }
  barstyle {
    id=209;
    name.LG_USENG = string("Bar style");
    default = straight;
    settings {
      straight {
        value=0;
        name.LG_USENG = string("Straight");
      }
      tilde {
        value=1;
        name.LG_USENG = string("Tilde");
      }
    }
  }

  atehtastyle {
    id=210;
    name.LG_USENG = string("A-tehta style");
    default = dots;
    settings {
      dots {
        value=0;
        name.LG_USENG = string("Dots");
      }
      circumflex {
        value=1;
        name.LG_USENG = string("Circumflex");
      }
    }
  }

  sarincestyle {
    id=211;
    name.LG_USENG = string("Sa-rince style");
    default = hook;
    settings {
      hook {
        value=0;
        name.LG_USENG = string("Hook");
      }
      swash {
        value=1;
        name.LG_USENG = string("Swash");
      }
    }
  }

endtable;

table(glyph)
  Wide = (ando,umbar,anga,ungwe,anto,ampa,anca,unque,nuumen,malta,noldo,nwalme,
          andoX,umbarX,angaX,ungweX,alda,maltaX,silme_aha,lambeN_alt,roomenN,tengwardigit3,tengwardigit6);
  UpNormal = (tinco,parma,roomen_alt,arda_alt);
  Normal = (calma,quesse,aha,hwesta,oore,anna,vala,vilya,
          calmaX,quesseX,roomen,arda,silmeN,esseN,
          hyarmen,yanta,hwestaS,uure,osseN,lambeN,osse,carrierX,quesseC,hwestaC,
          valaX,annaX,vaiya,tengwardigit2,tengwardigit5,tengwardigit9);
  DownNormal = (suule,formen);
  UpNarrow = (suule,formen,tincoX,parmaX,
          tehtaS_swash,tehtaS_swashhyarmen,tehtaS_swashhyarmenalt,tehtaS_swashlambe,tehtaS_swashraised,tehtaS_swashyanta,tehtaS_swashyantaalt);
  DownNarrow = (tinco,parma,tincoX,parmaX,roomen_alt,arda_alt,
          tengwardigit0,tengwardigit1,tengwardigit4,tengwardigit7,tengwardigit8,tengwarduodecimal10,tengwarduodecimal11,tengwarduodecimal12,
          tehtaS_swash,tehtaS_swashhyarmen,tehtaS_swashhyarmenalt,tehtaS_swashlambe,tehtaS_swashraised,tehtaS_swashyanta,tehtaS_swashyantaalt);
  Carrier = (longCarrier,halla,shortCarrier,hallaRoomen,aha_tinco,hwesta_tinco,
          tehtaS,tehtaS_aha,tehtaS_hyarmen,tehtaS_lambelow,tehtaS_lifted,tehtaS_raisedalt,tehtaS_yanta);

  AnyTengwa=(lambe,silme,esse,Wide,UpNormal,Normal,DownNormal,UpNarrow,DownNarrow,Carrier);

  Uptehta =       (tehtaA,        tehtaA_altshift2, tehtaY,        tehtaI,        tehtaE,        tehtaO,        tehtaU);
  UptehtaWide =   (tehtaA_shift4, tehtaA_altshift4, tehtaY_shift4, tehtaI_shift4, tehtaE_shift4, tehtaO_shift4, tehtaU_shift4);
  UptehtaNormal = (tehtaA_shift3, tehtaA_altshift3, tehtaY_shift3, tehtaI_shift3, tehtaE_shift3, tehtaO_shift3, tehtaU_shift3);
  UptehtaNarrow = (tehtaA_shift2, tehtaA_altshift2, tehtaY_shift2, tehtaI_shift2, tehtaE_shift2, tehtaO_shift2, tehtaU_shift2);
  UptehtaCarr =   (tehtaA_shift1, tehtaA_altshift1, tehtaY_shift1, tehtaI_shift1, tehtaE_shift1, tehtaO_shift1, tehtaU_shift1);

  Uptehta +=       (tehtaN_narrow, tehtaN_narrowlow, tehtaN_altnarrow, tehtaN_altnarrowlow, tehtaW,        tehtaW_combshift2, tehtaYanta,        tehtaAE,        tehtaAE_altshift2);
  UptehtaWide +=   (tehtaN_wide,   tehtaN_widelow,   tehtaN_altwide,   tehtaN_altwidelow,   tehtaW_shift4, tehtaW_combshift4, tehtaYanta_shift4, tehtaAE_shift4, tehtaAE_altshift4);
  UptehtaNormal += (tehtaN_narrow, tehtaN_narrowlow, tehtaN_altnarrow, tehtaN_altnarrowlow, tehtaW_shift3, tehtaW_combshift3, tehtaYanta_shift3, tehtaAE_shift3, tehtaAE_altshift3);
  UptehtaNarrow += (tehtaN_narrow, tehtaN_narrowlow, tehtaN_altnarrow, tehtaN_altnarrowlow, tehtaW_shift2, tehtaW_combshift2, tehtaYanta_shift2, tehtaAE_shift2, tehtaAE_altshift2);
  UptehtaCarr +=   (tehtaN_narrow, tehtaN_narrowlow, tehtaN_altnarrow, tehtaN_altnarrowlow, tehtaW_shift1, tehtaW_combshift1, tehtaYanta_shift1, tehtaAE_shift1, tehtaAE_altshift1);

  Uptehta +=       (tehtaBreve,        tehtaGrave,        tehtaO_tehtaI_shift2, tehtaU_tehtaI_shift2, tehtaE_tehtaE_shift2, tehtaO_tehtaO_shift2, tehtaU_tehtaU_shift2);
  UptehtaWide +=   (tehtaBreve_shift4, tehtaGrave_shift4, tehtaO_tehtaI_shift4, tehtaU_tehtaI_shift4, tehtaE_tehtaE_shift4, tehtaO_tehtaO_shift4, tehtaU_tehtaU_shift4);
  UptehtaNormal += (tehtaBreve_shift3, tehtaGrave_shift3, tehtaO_tehtaI_shift3, tehtaU_tehtaI_shift3, tehtaE_tehtaE_shift3, tehtaO_tehtaO_shift3, tehtaU_tehtaU_shift3);
  UptehtaNarrow += (tehtaBreve_shift2, tehtaGrave_shift2, tehtaO_tehtaI_shift2, tehtaU_tehtaI_shift2, tehtaE_tehtaE_shift2, tehtaO_tehtaO_shift2, tehtaU_tehtaU_shift2);
  UptehtaCarr +=   (tehtaBreve_shift1, tehtaGrave_shift1, tehtaO_tehtaI_shift1, tehtaU_tehtaI_shift1, tehtaE_tehtaE_shift1, tehtaO_tehtaO_shift1, tehtaU_tehtaU_shift1);

  Downtehta =       (tehtaAB,        tehtaYB,        tehtaIB,        tehtaEB,        tehtaEEB,        tehtaOB,        tehtaUB);
  DowntehtaWide =   (tehtaAB_shift4, tehtaYB_shift4, tehtaIB_shift4, tehtaEB_shift4, tehtaEEB_shift4, tehtaOB_shift4, tehtaUB_shift4);
  DowntehtaNormal = (tehtaAB_shift3, tehtaYB_shift3, tehtaIB_shift3, tehtaEB_shift3, tehtaEEB_shift3, tehtaOB_shift3, tehtaUB_shift3);
  DowntehtaNarrow = (tehtaAB_shift2, tehtaYB_shift2, tehtaIB_shift2, tehtaEB_shift2, tehtaEEB_shift2, tehtaOB_shift2, tehtaUB_shift2);
  DowntehtaCarr =   (tehtaAB_shift1, tehtaYB_shift1, tehtaIB_shift1, tehtaEB_shift1, tehtaEEB_shift1, tehtaOB_shift1, tehtaUB_shift1);
  DowntehtaLambe =  (tehtaAB_shift4, tehtaYB_lambe,  tehtaIB_lambe,  tehtaEB_shift4, tehtaEEB_lambe,  tehtaOB_shift4, tehtaUB_shift4);

  Downtehta +=       (tehtaB_narrow, tehtaB_altnarrow, tehtaThinnas,        tehtaX,        tengwardigitMark);
  DowntehtaWide +=   (tehtaB_wide,   tehtaB_altwide,   tehtaThinnas_shift4, tehtaX_shift4, tengwardigitMark_shift4);
  DowntehtaNormal += (tehtaB_narrow, tehtaB_altnarrow, tehtaThinnas_shift3, tehtaX_shift3, tengwardigitMark_shift3);
  DowntehtaNarrow += (tehtaB_narrow, tehtaB_altnarrow, tehtaThinnas_shift2, tehtaX_shift3, tengwardigitMark_shift2);
  DowntehtaCarr +=   (tehtaB_narrow, tehtaB_altnarrow, tehtaThinnas_shift1, tehtaX_shift3, tengwardigitMark_shift1);
  DowntehtaLambe +=  (tehtaB_lambe,  tehtaB_altlambe,  tehtaThinnas_shift4, tehtaX_shift4, tengwardigitMark_shift4);

  AnyIsolTehta = (Uptehta,Downtehta);
  AnyTehta = (UptehtaWide,UptehtaNormal,UptehtaNarrow,UptehtaCarr,DowntehtaWide,DowntehtaNormal,DowntehtaNarrow,DowntehtaCarr,DowntehtaLambe);

  RightCombShook = (tinco,parma,ando,umbar,suule,formen,anto,ampa,tincoX,parmaX,andoX,umbarX,nuumen,malta,oore,vala,hallaRoomen,aha_tinco,hwesta_tinco);
  WideCombShook = (anga,ungwe,anca,unque,angaX,ungweX,noldo,nwalme);

//  LiftedShook = (calma,anga,aha,hwesta,anca,unque,calmaX,quesseX,angaX,ungweX,noldo,anna,hyarmen,yanta,halla,annaX,quesseC,vaiya,lambeN,roomenN);
//  RaisedShook = (quesse,ungwe,nwalme,vilya,roomen,arda,silmeN,hwestaS,hallaRoomen,osse,shortCarrier,longCarrier,carrierX);
//  LambeswashShook = (lambe,alda,esse);

endtable;

#define AnyIsolTehtar [[[[AnyIsolTehta]?AnyIsolTehta]?AnyIsolTehta]?AnyIsolTehta]?
#define AnyTehtar [[[AnyTehta]?AnyTehta]?AnyTehta]?

table(substitution)

  pass(1)

    if (barstyle==straight)
      tehtaN > tehtaN_narrow / _ ;
      tehtaB > tehtaB_narrow / _ ;
    elseif (barstyle==tilde)
      tehtaN > tehtaN_altnarrow / _ ;
      tehtaB > tehtaB_altnarrow / _ ;
    endif;

    if (descwtengwa)
      lambeN > lambeN_alt;
    endif;

    if (ossetail==all || ossetail==final)
      osse > osse_alt ;
    endif;

    if (atehtastyle==circumflex)
      tehtaA > tehtaA_altshift2;
      tehtaAE > tehtaAE_altshift2;
    endif;

    aha ZWJ tinco > aha_tinco:(1 2 3) _ _ ;
    hwesta ZWJ tinco > hwesta_tinco:(1 2 3) _ _ ;
    silme ZWJ aha > silme_aha:(1 2 3) _ _ ;
    tehtaI tehtaO > tehtaO_tehtaI_shift2:(1 2) _ ;
    tehtaO tehtaI > tehtaO_tehtaI_shift2:(1 2) _ ;
    tehtaI tehtaU > tehtaU_tehtaI_shift2:(1 2) _ ;
    tehtaU tehtaI > tehtaU_tehtaI_shift2:(1 2) _ ;
    tehtaE tehtaE > tehtaE_tehtaE_shift2:(1 2) _ ;
    tehtaO tehtaO > tehtaO_tehtaO_shift2:(1 2) _ ;
    tehtaU tehtaU > tehtaU_tehtaU_shift2:(1 2) _ ;
    ZWJ longCarrier > tehtaThinnas:(1 2) _ ;
    tehtaI tehtaI > tehtaY:(1 2) _ ;

    if (sarincestyle==hook)
       tehtaS > tehtaS_lifted / (calmaX,quesseX,angaX,ungweX,halla,quesseC,vaiya,lambeN,roomenN) AnyTehtar _ ;
       tehtaS > tehtaS_aha / (aha,hwesta,anca,unque,noldo,anna,nwalme,vilya,annaX) AnyTehtar _ ;
       tehtaS > tehtaS_hyarmen / (hyarmen) AnyTehtar _ ;
       tehtaS > tehtaS_lambelow / (lambe,esse) AnyTehtar _ ;
       tehtaS > tehtaS_raisedalt / (calma,anga,quesse,ungwe,roomen,arda,alda,silmeN,hwestaS,uure,hallaRoomen,osse,longCarrier,carrierX) AnyTehtar _ ;
       tehtaS > tehtaS_yanta / (yanta) AnyTehtar _ ;
    elseif (sarincestyle==swash)
       tehtaS > tehtaS_swash / (tinco,parma,ando,umbar,suule,formen,anto,ampa,nuumen,malta,oore,vala,tincoX,parmaX,andoX,umbarX,alda,silme,esseN,shortCarrier,osseN) AnyTehtar _ ;
       tehtaS > tehtaS_swash / (halla,quesseC,vaiya,lambeN,roomenN) AnyTehtar _ ;
       tehtaS > tehtaS_swash / (aha,hwesta,anca,unque,noldo,anna,nwalme,vilya,annaX) AnyTehtar _ ;
       tehtaS > tehtaS_swashhyarmen / (hyarmen) AnyTehtar _ ;
       tehtaS > tehtaS_swashyanta / (yanta) AnyTehtar _ ;
       tehtaS > tehtaS_swashlambe / (lambe,esse) AnyTehtar _ ;
       tehtaS > tehtaS_swashraised / (calma,anga,quesse,ungwe,roomen,arda,silmeN,hwestaS,uure,hallaRoomen,osse,longCarrier,carrierX,calmaX,quesseX,angaX,ungweX) AnyTehtar _ ; 
    endif;

	tehtaW > tehtaW_combshift2 / _ AnyTehtar (tehtaA,tehtaY,tehtaI,tehtaE,tehtaO,tehtaU,tehtaW,tehtaYanta,tehtaAE,tehtaBreve,tehtaGrave,tehtaO_tehtaI_shift2,tehtaU_tehtaI_shift2) ;
	tehtaW > tehtaW_combshift2 / (tehtaA,tehtaY,tehtaI,tehtaE,tehtaO,tehtaU,tehtaW,tehtaYanta,tehtaAE,tehtaBreve,tehtaGrave,tehtaO_tehtaI_shift2,tehtaU_tehtaI_shift2) AnyTehtar _ ;

  endpass;
  pass(2)

    (tehtaN_narrow,tehtaN_altnarrow) > (tehtaN_narrowlow,tehtaN_altnarrowlow) / ^ _ AnyTehtar Uptehta ;
    (tehtaN_narrow,tehtaN_altnarrow) > (tehtaN_narrowlow,tehtaN_altnarrowlow) / Uptehta AnyTehtar _ ;

  endpass;
  pass(3)

    if (ossetail==final)
       (osse_alt) > (osse) / ^ _ AnyIsolTehtar ZWJ? AnyTengwa;
    endif;

    (roomen,arda) > (roomen_alt,arda_alt) / _ AnyIsolTehtar Downtehta ;

    tehtaX > tehtaX_shift4 / WideCombShook AnyTehtar _ ;
    tehtaX > tehtaX_down / RightCombShook AnyTehtar _ ;
//    tehtaS > tehtaS_lifted / LiftedShook AnyTehtar _ ;
//    tehtaS > tehtaS_raised / RaisedShook AnyTehtar _ ;
//    tehtaS > tehtaS_swashlambe / LambeswashShook AnyTehtar _ ;

    (tehtaS_swashhyarmen,tehtaS_swashyanta) > (tehtaS_swashhyarmenalt,tehtaS_swashyantaalt) / _ AnyIsolTehtar AnyIsolTehta ;

    Uptehta > UptehtaWide / (Wide,lambe,silme,esse) AnyTehtar _ ;
    Uptehta > UptehtaNormal / (UpNormal,Normal) AnyTehtar _ ;
    Uptehta > UptehtaNarrow / UpNarrow AnyTehtar _ ;
    Uptehta > UptehtaCarr / Carrier AnyTehtar _ ;

    Downtehta > DowntehtaWide / Wide AnyTehtar _ ;
    Downtehta > DowntehtaNormal / (silme,esse,DownNormal,Normal) AnyTehtar _ ;
    Downtehta > DowntehtaNarrow / DownNarrow AnyTehtar _ ;
    Downtehta > DowntehtaCarr / Carrier AnyTehtar _ ;
    Downtehta > DowntehtaLambe / lambe AnyTehtar _ ;

  endpass;
  

endtable;

table(position)
  pass(1)
    // This is a workaround for what seems to be a bug in XeTeX:
    AnyTehta { advance.x = 0; };
  endpass;
endtable;

endenvironment;

