%% Tengwar Telcontar - a Unicode Tengwar font
%% Copyright (c) 2005-2010 Johan Winge
%% 
%% Tengwar Telcontar is free software: you can redistribute it and/or
%% modify it under the terms of the GNU General Public License as
%% published by the Free Software Foundation, either version 3 of
%% the License, or (at your option) any later version.
%% 
%% As a special exception, if you create a document which uses this font,
%% and embed this font or unaltered portions of this font into the
%% document, this font does not by itself cause the resulting document
%% to be covered by the GNU General Public License. This exception does
%% not however invalidate any other reasons why the document might be
%% covered by the GNU General Public License. If you modify this font,
%% you may extend this exception to your version of the font, but you are
%% not obligated to do so. If you do not wish to do so, delete this
%% exception statement from your version.
%% 
%% This program is distributed in the hope that it will be useful,
%% but WITHOUT ANY WARRANTY; without even the implied warranty of
%% MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
%% GNU General Public License for more details.
%% 
%% You should have received a copy of the GNU General Public License
%% along with this program.  If not, see <http://www.gnu.org/licenses/>.


% The following higher level commands defined herein can be used in a program:
%
%   beginchr(s, width)               Begin a normal PostScript character, named s.
%   beginrefchr(s, width)            Referenced character. Note: only setap and attach useful inside! No drawing.
%   endchr                           End both beginchr and beginrefchr.
%   setspace(width)                  To set the space character of the font, simply.
%   setap(s, p)                      Define attachpoint named s at p in the current char.
%   attach(psn, bapn, capn)          Attach PS character named psn to attachpoint bapn with attachpoint capn.
%   combinechr(s,bcn,bapn,ccn,capn)  Shortcut to build simple referenced char.
%   exit                             End project.

tracingtitles:=1;
boolean tracing; tracing=(mode=pixpt);

scalefactor=1.25; % Should match scale factor in tengtelc.pe

un#:=1/2048em#; define_pixels(un);
if tracing:
  message "gd: table(glyph) { MUnits = 2048; PointRadius = 0m; }";
  message "pe: New();";
  message "pe: ScaleToEm(2048);";
  message "pe: SelectAll();";
  message "pe: Import($1+" & ditto & ".pk" & ditto & ");";
  message "pe: LoadEncodingFile($1+" & ditto & ".enc" & ditto & ");";
  message "pe: Reencode($1,1);";
  message "pe: SetFontNames(" & ditto & font_ps_name & ditto & ",\"; % These should be defined.
  message "pe:  " & ditto & font_name & ditto & ",\";
  message "pe:  " & ditto & font_name & ditto & ",\";
  message "pe:  " & ditto & font_weight & ditto & ",\";
  message "pe:  " & ditto & font_copyright & ditto & ",\";
  message "pe:  " & ditto & font_version & ditto & ");";
fi

numeric ps.n; ps.n:=0;    % Character counter
string ps[].name;         % Postscript names of character
picture ps[].pic;         % The bitmap picture (for gftodvi preview)
numeric ps[].width;       % Width
numeric ps[].ap.n;        % Attachment Point counter
string ps[].ap[].name;    % Names of AP
pair ps[].ap[].pt;        % The position of the AP
boolean ps[].ap[].forgd;  % Whether the AP should be output to Graphite or not
mf_char_c:=-1;            % Metafont character counter
ref_char_c:=255;          % Reference character counter
boolean isrefchar;
boolean forgd; forgd:=true; % Output gdh-data?
boolean unlinkreferences; unlinkreferences:=false;
boolean hasattached;
boolean usealiasnames; usealiasnames:=true;

% Name lookup (return index)
vardef ps.lu(expr s) =
  save i; i:=0;
  for j=1 upto ps.n:
    if (ps[j].name = s): i:=j; fi
  endfor
  i
enddef;

% Lookup attachpoint name
vardef first.ps[]@#=@ enddef;  % (See MFbook exercise 20.5)
vardef ps[].ap.lu(expr s) =   
  save i; i:=0;
  for j=1 upto ps[first#@].ap.n:
    if ps[first#@].ap[j].name = s: i:=j; fi
  endfor
  i
enddef;

def psc(expr psn) =           % A shortcut
  ps[if string psn: ps.lu(psn) else: psn fi]
enddef;

def lastap = whatever enddef; % Ugly...
def apt(expr psn, apn) =
  psc(psn).ap[if unknown apn: psc(psn).ap.n elseif string apn: psc(psn).ap.lu(apn) else: apn fi]
enddef;

vardef transformadjust(expr in) =
  save out;
  if pair in:
    pair out; out:=in transformed currenttransform;
  elseif numeric in:
    numeric out; out:=xpart((in,0) transformed currenttransform);
  fi
  out
enddef;

def padding(expr l, r) =
  lpad:=l; rpad:=r; w:=w+l+r;
  setap("lpad",(l,0)); apt(ps.n,lastap).forgd :=false;
  setap("rpad",(w-r,0)); apt(ps.n,lastap).forgd :=false;
enddef;

def beginchr(expr s, width) =
  isrefchar:=false;
  initchr(s, incr mf_char_c, width);
enddef;

def beginrefchr(expr s, width) =
  isrefchar:=true;
  initchr(s, if not tracing: incr mf_char_c else: 0 fi, width);
enddef;

def initchr(expr s, m, width) =
  if isrefchar and not tracing or not isrefchar:
    beginchar(m, width/hppp, asc#, desc#);
  else:
    w:=width;
    numeric x[],y[];
  fi
  ps[incr ps.n].name := s;
  ps[ps.n].ap.n := 0;
  setap("origo",(0,0));
  apt(ps.n,lastap).forgd := false;
  numeric a[], t; path p[], ap[];
  if tracing and forgd:
    if usealiasnames:
      message "gd: " & cleandots(ps_to_alias(ps[ps.n].name)) & " = ps(" & ditto & ps_to_alias(ps[ps.n].name) & ditto & ") { ";
    else:
      message "gd: " & cleandots(ps_to_alias(ps[ps.n].name)) & " = ps(" & ditto & ps[ps.n].name & ditto & ") { ";
    fi
  fi
  hasattached:=false;
enddef;

def endchr =
  if tracing:
    message "pe: Select("& ditto & ps[ps.n].name & ditto &"); SetWidth(" & decimal round(transformadjust(w)/un) & ");";
    if usealiasnames:
      message "pe: SetGlyphName("& ditto & ps_to_alias(ps[ps.n].name) & ditto &",0);";
    fi
    if isrefchar:
      if unlinkreferences:
        if usealiasnames:
          if tracing: message "ur: Select("& ditto & ps_to_alias(ps[ps.n].name) & ditto &"); UnlinkReference(); RemoveOverlap();"; message""; fi
        else:
          if tracing: message "ur: Select("& ditto & ps[ps.n].name & ditto &"); UnlinkReference(); RemoveOverlap();"; message""; fi
        fi
      fi
    else:
      message "pe: Autotrace(); Scale(1000,0,0); AddExtrema(); Scale(10,0,0);";
      message "pe: Simplify(0,1,0,0,1,5000); Simplify(2,1,0,0,1,5000);";
      if hasattached:
         if tracing: message "pe: UnlinkReference(); RemoveOverlap();"; message""; fi
      fi
    fi
  fi
  ps[ps.n].width := w;
  setap("right",(w,0));
  apt(ps.n,lastap).forgd := false;
  for i=1 upto psc(ps.n).ap.n:
    if apt(ps.n,i).forgd:
      if tracing and forgd: message "gd:  " & apt(ps.n,i).name & " = point("
         & decimal round(scalefactor*xpart transformadjust(apt(ps.n,i).pt)/un) & "m,"
         & decimal round(scalefactor*ypart transformadjust(apt(ps.n,i).pt)/un) & "m);"; fi
      if not tracing: makelabel(apt(ps.n,i).name,apt(ps.n,i).pt); fi
    else:
      if not tracing: makelabel("("& apt(ps.n,i).name &")",apt(ps.n,i).pt); fi
    fi
  endfor
  if not tracing:
    cullit;
    w:=transformadjust(w);
    ps[ps.n].pic := currentpicture;
    ps[ps.n].name;
    proofrule((0,ex),(w,ex));
  else:
    if forgd: message "gd: };"; fi
    if usealiasnames:
       message "pe: Print(" & ditto & "Done " & ps_to_alias(ps[ps.n].name) & ditto & ");";
    else:
       message "pe: Print(" & ditto & "Done " & ps[ps.n].name & ditto & ");";
    fi
    message "ec: " & ps[ps.n].name if isrefchar: & " [" & decimal(incr ref_char_c) & "]" fi;
  fi
  if isrefchar and not tracing or not isrefchar:
    endchar;
  fi
enddef;

numeric gdalias.n; gdalias.n := 0;
string gdalias[].psname;
string gdalias[].gdname;

vardef gdalias.lu(expr s) =
  save i; i:=0;
  for j=1 upto gdalias.n:
    if (gdalias[j].psname = s): i:=j; fi
  endfor
  i
enddef;

def setgdalias(expr psn, gdn) =
  gdalias[incr gdalias.n].psname=psn;
  gdalias[gdalias.n].gdname=gdn;
enddef;

def addalias(expr psn, gdn) =
  scantokens ("string " & cleandots(gdn)); scantokens cleandots(gdn)=psn; setgdalias(psn,gdn);
enddef;

def addaliasvars(expr balias)(text vars) =
  for i=vars:
    setgdalias(scantokens balias & "." & i , balias & "." & i);
  endfor
enddef;

vardef ps_to_alias(expr s) =
  save n,ss; string ss;
  n=gdalias.lu(s);
  ss = if n=0: s else: gdalias[n].gdname fi;
  ss
enddef;

vardef cleandots(expr s) =
  save c,ss; string c,ss;
  ss:="";
  for i=0 upto length(s)-1:
    c:=substring(i,i+1) of s;
    if c=".": c:="_"; fi
    ss:= ss & c;
  endfor
  ss
enddef;

def setspace(expr width) =
%  font_normal_space width/hppp;
  beginrefchr("space",width);
    if tracing: message "gd:  break = BREAK_WHITESPACE;"; fi
  endchr;
enddef;

def setap(expr s, p) =
  ps[ps.n].ap.n:=ps[ps.n].ap.n+1;
  apt(ps.n,lastap).name := s;
  apt(ps.n,lastap).pt := p;
  apt(ps.n,lastap).forgd :=true;
enddef;

def attach(expr psn, bapn, capn) =
  pair vec;
  vec=apt(ps.n,bapn).pt-apt(psn,capn).pt;
  if psc(psn).ap.lu(bapn)=0: apt(ps.n,bapn).forgd:=false; fi
  for i=1 upto psc(psn).ap.n:
    j:=ps[ps.n].ap.lu(apt(psn,i).name);
    if j=0:
      setap(apt(psn,i).name,apt(psn,i).pt+vec);
      if (apt(psn,i).name=capn) or (apt(psn,i).forgd=false): apt(ps.n,lastap).forgd:=false; fi
    else:
      if apt(psn,i).name=capn: 
        if capn=bapn: apt(ps.n,j).forgd:=false; fi
      else:
        if (apt(psn,i).name=bapn) and not tracing: makelabel("",apt(ps.n,j).pt); fi
        apt(ps.n,j).pt:=apt(psn,i).pt+vec;
      fi
    fi
  endfor
  if tracing:
    if usealiasnames:
      message "pe: Select("& ditto & ps_to_alias(psn) & ditto &"); CopyReference();";
    else:
      message "pe: Select("& ditto & psn & ditto &"); CopyReference();";
    fi
    message "pe: Select("& ditto & ps[ps.n].name & ditto &"); PasteWithOffset(" &
         decimal round(xpart transformadjust(vec)/un) & "," & decimal round(ypart transformadjust(vec)/un) & ");";
  else:
    addto currentpicture also psc(psn).pic shifted transformadjust(vec);
  fi
  hasattached:=true;
enddef;

def combinechr(expr s,bcn,bapn,ccn,capn) =
  beginrefchr(s,psc(bcn).width);
    attach(bcn,"origo","origo");
    attach(ccn,bapn,capn);
  endchr;
enddef;

def exit =
  if tracing:
    message "gd: endtable;"; message "";
    message "pe: Save($1+" & ditto & ".sfd" & ditto & ");";
    message "pe: Quit(0);";
    message "";
  fi
enddef;

% Given a name of an attachpoint (or anchor point, as they are usually known) and a list of chars,
% this function calculates the average position of the attachpoint in these chars,
% relative to the right hand side of the chars.
vardef averageap(expr apname)(text chars) =
  save i, psn, num, attachpt, psnames, avg;
  pair attachpt[], avg;
  string psn, psnames[];
  num:=0; avg:=(0,0);
  for psn = chars:
     psnames[incr num]=psn;
     if ps.lu(psn)=0:
        message "Warning: Unknown character "&psn;
        attachpt[num]=(0,0);
     else:
        attachpt[num]=(apt(psn,apname).pt-(psc(psn).width,0));
     fi
  endfor
  for i=1 upto num:
     avg:=avg + attachpt[i]/num;
  endfor
  for i=1 upto num:
     if abs(avg-attachpt[i]) > 0.05pt:
       message "Warning: " & apname & " of " & psnames[i] & " is " & decimal(abs(avg-attachpt[i])/pt) & "pt from the average.";
     fi
  endfor
  avg
enddef;

def shiftchr(expr s, chr, ap, disp) =
  beginrefchr(s,psc(chr).width);
    setap("shiftap",disp);
    attach(chr,"shiftap",ap);
  endchr;
enddef;

if tracing: message ""; fi
