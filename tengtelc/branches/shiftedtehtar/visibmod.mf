input setup;
font_ps_name:="visibmod";
font_name:="Visible Modifiers";
input regular;

numeric le;
le:=1.4ex/20;
hg:=5le;

%addalias("uni25A1","dottedbox");
%addalias("uniE000","dottedbox");
addalias("dottedbox","dottedbox");
% These are the code points used in the Unicode Standard PDF files:
addalias("uniE0A0","nbsp");
addalias("uniE20B","zwsp");
addalias("uniE20C","zwnj");
addalias("uniE20D","zwj");

beginchr(dottedbox,20le);
  padding(2le,2le);
  pickup pensquare scaled 1le;
  bot lft z1=(lpad,0); top lft z2=(lpad,20le);
  top rt z3=(w-rpad,20le); bot rt z4=(w-rpad,0);
  draw z1--z2--z3--z4--z1;
  for i=0 upto 4:
    t:=1/8 + i*(1-2/8)/4;
    pickup pensquare xscaled 2le yscaled 1le;
    erase drawdot t[z1,z2];
    erase drawdot t[z3,z4];
    pickup pensquare xscaled 1le yscaled 2le;
    erase drawdot t[z2,z3];
    erase drawdot t[z4,z1];
  endfor
endchr;
beginchr("B",6le);
  pickup pencircle scaled 1le;
  x1=x2=1le;
  y2=1le;
  y1=y2+hg;
  z3=1/2[z2,z1];
  y4=1/2[y3,y1];
  x4=x5-0.5le;
  x5=x1+4le;
  y5=1/2[y2,y3];
  draw z1--z2;
  draw z1{right}...z4{down}...z3{left};
  draw z3{right}...z5{down}...z2{left};
endchr;
pause;
beginchr("J",6le);
  pickup pencircle scaled 1le;
  x5=1le;
  x2=x3=x5+4le;
  y1=y2;
  x1=x2-1le;
  x4=1/2[x5,x3];
  y5=y3=2/5[y4,y2];
  y4=1le;
  y2=y4+hg;
  draw z1--z2--z3...z4{left}...z5{up};
endchr;
beginchr("N",6le);
  pickup pencircle scaled 1le;
  x1=x2=1le;
  x3=x4=x1+4le;
  y1=y3=1le;
  y2=y4=y1+hg;
  draw z1--z2--z3--z4;
endchr;
beginchr("P",6le);
  pickup pencircle scaled 1le;
  x1=x2=1le;
  y2=1le;
  y1=y2+hg;
  z3=2/5[z2,z1];
  y4=1/2[y3,y1];
  x4=x1+4le;
  draw z1--z2;
  draw z1{right}...z4{down}...z3{left};
endchr;
pause;
beginchr("S",6le);
  pickup pencircle scaled 1le;
  x3=x6=1le;
  x1=x4=x3+4le;
  y5=1le;
  y2=y5+hg;
  y2-y1=y6-y5=1le;
  y2-y3=y4-y5=1le;
  x2=x5=1/2[x6,x1];
  draw z1{up}...z2{left}...z3{down}...z4{down}...z5{left}...z6{up};
endchr;
beginchr("W",8le);
  pickup pencircle scaled 1le;
  x1=1le;
  x5=x1+6le;
  y1=y5=y2+hg;
  y2=y4=1le;
  x2=1/2[x1,x3]; x4=1/2[x3,x5];
  x3=1/2[x1,x5];
  y3=0.8[y2,y1];
  draw z1--z2--z3--z4--z5;
endchr;
beginchr("Z",6le);
  pickup pencircle scaled 1le;
  x1=x4=x5=1le;
  x2=x3=x6=x1+4le;
  y5=y6=1le;
  y1=y2=y5+hg;
  y2-y3=y4-y5=1/2le;
  draw z1--z2--z3--z4--z5--z6;
endchr;

beginrefchr(zwj,psc(dottedbox).width);
  attach(dottedbox,"origo","origo");
  x1=5le; x2=11le;
  y1=y2=y3+hg+3le;
  y3=2le;
  x3=8le;
  setap("ap1",z1); attach("Z","ap1","origo");
  setap("ap2",z2); attach("W","ap2","origo");
  setap("ap3",z3); attach("J","ap3","origo");
endchr;
pause;

beginrefchr(zwnj,psc(dottedbox).width);
  attach(dottedbox,"origo","origo");
  x1=5le; x2=11le;
  y1=y2=y3+hg+3le;
  y3=y4=2le;
  x3=6le; x4=12le;
  setap("ap1",z1); attach("Z","ap1","origo");
  setap("ap2",z2); attach("W","ap2","origo");
  setap("ap3",z3); attach("N","ap3","origo");
  setap("ap4",z4); attach("J","ap4","origo");
endchr;
pause;

beginrefchr(zwsp,psc(dottedbox).width);
  attach(dottedbox,"origo","origo");
  x1=5le; x2=11le;
  y1=y2=y3+hg+3le;
  y3=y4=2le;
  x3=5.5le; x4=12.5le;
  setap("ap1",z1); attach("Z","ap1","origo");
  setap("ap2",z2); attach("W","ap2","origo");
  setap("ap3",z3); attach("S","ap3","origo");
  setap("ap4",z4); attach("P","ap4","origo");
endchr;
pause;

beginrefchr(nbsp,psc(dottedbox).width);
  attach(dottedbox,"origo","origo");
  x1=5.5le; x2=12.5le;
  y1=y2=y3+hg+3le;
  y3=y4=2le;
  x3=5.5le; x4=12.5le;
  setap("ap1",z1); attach("N","ap1","origo");
  setap("ap2",z2); attach("B","ap2","origo");
  setap("ap3",z3); attach("S","ap3","origo");
  setap("ap4",z4); attach("P","ap4","origo");
endchr;
pause;

exit;
end.

